REM /**
REM  * WinTask.bbj
REM  * @author gosteen
REM  *C:\Users\gosteen\eclipse\workspaces\bbj-workspace\TaskApp\WinTask.bbj
REM  */
rem package TaskApp

class public TaskProperties
    field public TaskApp           app!
    field public BBjSysGui         sysgui!
    field public BBjTopLevelWindow winTaskProperties!
    field public BBjEditBox        editTitle!
    field public BBjCEdit          editDescription!
    field public BBjListButton     listStatus!
    field public BBjListButton     listPriority!
    field public BBjInputD         dateDue!

    field public BBjChildWindow    winButtons!
    field public BBjButton         buttonCancel!
    field public BBjButton         buttonSave!

    field public Task              task!

    method public TaskProperties(TaskApp app!)
        #task! = null()
        #app! = app!
        #sysgui! = BBjAPI().getSysGui()
        #winTaskProperties! = BBjAPI().getSysGui().addWindow(#sysgui!.getAvailableContext(), "winTask", $011F0000$)
        #winTaskProperties!.addClass("winTaskProperties")

        #editTitle! = #winTaskProperties!.addEditBox("")
        #editTitle!.setAttribute("label", "Title")
        #editTitle!.setPlaceholder("Task Title")

        #editDescription! = #winTaskProperties!.addCEdit("", $0002$)
        #editDescription!.setAttribute("label", "Description")
        #editDescription!.setPlaceholder("Task Description")

        #listStatus! = #winTaskProperties!.addListButton("")
        #listStatus!.setAttribute("label", "Status")

        #listPriority! = #winTaskProperties!.addListButton("")
        #listPriority!.setAttribute("label", "Priority")
        for i = Task.PRIORITY_MIN() to Task.PRIORITY_MAX()
            if (i = 0) then
                #listPriority!.addItem("No Priority")
            else
                #listPriority!.addItem("Priority " + str(i))
            endif
        next i

        for i = 0 to 3
            s! = Task.getStatusString(i)
            s! = s!.substring(0,1).toUpperCase() + s!.substring(1)
            #listStatus!.addItem(s!)
        next i

        #dateDue! = #winTaskProperties!.addInputD()
        #dateDue!.setAttribute("label", "Due Date")
        #dateDue!.setText("")
        #dateDue!.setAttribute("visible-calender-icon","true")
        #dateDue!.setAttribute("show-weeks","true")


        rem Long Year
        REM short_year$ = STBL("!COMPAT!", "SHORT_YEAR=TRUE")
        REM DateMask$=stbl("!DATE"),DateMask=pos($00$=DateMask$),DateMask$=DateMask$(1,DateMask-1)
        REM #dateDue!.setLocale("en-US")
        REM #dateDue!.setMask(DateMask$)
        REM a=msgbox("Date Mask: " + DateMask$ + $0a$ + "!COMPAT for SHORT_YEAR: " + STBL("!COMPAT"))



        #winButtons! = #winTaskProperties!.addChildWindow("", $00108800$, #sysgui!.getAvailableContext())
        #buttonCancel! = #winButtons!.addButton("Cancel")

        #buttonSave!   = #winButtons!.addButton("Save")
        #buttonSave!.setAttribute("theme","primary")

        rem Callbacks
        #buttonCancel!.setCallback(BBjControl.ON_BUTTON_PUSH, #this!, "onCancel")
        #buttonSave!.setCallback(BBjControl.ON_FORM_VALIDATION, #this!, "onSave")
        rem populate fields
        #winTaskProperties!.setVisible(1)
    methodend

    rem /**
    rem  * Given a task object, load its properties into the Task Properties window
    rem  */
    method public void loadTask(Task t!)
        rem Clone the task, so it can't directly modify it
        #task! = new Task(t!)
        #editTitle!.setText(#task!.getTitle())
        #editDescription!.setText(#task!.getDescription())
        dueDate! = #task!.getDueDate()
        if (dueDate! > 0) then
            #dateDue!.setValue(#task!.getDueDate())
        else
            #dateDue!.setValue(-1)
        endif

        #listPriority!.selectIndex(t!.getPriority())
        #listStatus!.selectIndex(t!.getStatus())
    methodend

    rem /**
    rem  * Save the information from the current fields into the Task object in the #task! variable
    rem  */
    method public void onSave(BBjFormValidationEvent e!)
        if (#task! = null()) then methodret

        rem validate title
        title! = e!.getText(#editTitle!)
        if (title!.trim().length() = 0) then
            e!.accept(0)
            tmp = msgbox("Title must not be empty.",BBjSysGui.MSGBOX_ICON_EXCLAMATION,"Invalid Title",mode="theme=warning")
            methodret
        endif

        rem validate date
        date! = e!.getValue(#dateDue!)
        today! = jul(date(0))
        limit! = 5*365
        difference! = date!-today!

        if (abs(difference!) > limit!) AND (date! <> -1) then
            rem date out of bounds
            e!.accept(0)
            message! = "Date cannot be more than 5 years in the "
            if (difference! < 0) then
                message! = message! + "past."
            else
                message! = message! + "future."
            endif

            tmp = msgbox(message!,BBjSysGui.MSGBOX_ICON_EXCLAMATION,"Invalid Date",mode="theme=warning")
            methodret
        endif

        rem passed validations; update task
        #task!.setTitle(title!)
        #task!.setDescription(e!.getText(#editDescription!))
        #task!.setDueDate(date!)
        #task!.setPriority(e!.getSelectedIndex(#listPriority!))
        #task!.setStatus(e!.getSelectedIndex(#listStatus!))

        #app!.updateTask(#task!)
        #winTaskProperties!.setVisible(0)
        e!.accept(1)
    methodend

    method public void onCancel(BBjButtonPushEvent e!)
        #winTaskProperties!.setVisible(0)
    methodend

    method public void setVisible(BBjNumber v)
        #winTaskProperties!.setVisible(v)
    methodend

classend

use ::TaskApp.bbj::TaskApp
use ::Task.bbj::Task

use java.time.LocalDate
