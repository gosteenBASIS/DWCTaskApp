REM /**
REM  * TaskCardView.bbj
REM  * @author gosteen, ndecker, bhighhill
REM  *
REM  * TaskCardView represents the childwindow of a task view
REM  */
REM package TaskApp

class public TaskCardView extends TaskView
    field protected BBjButton       btnDeleteTask!
    field protected BBjButton       btnLock!
    field protected BBjButton       btnSetComplete!
    field protected BBjString       lock! = "<html><dwc-icon pool='tabler' name='lock' style='width: 30px; height: 30px'></dwc-icon>"
    field protected BBjString       lockOpen! = "<html><dwc-icon pool='tabler' name='lock-open' style='width: 30px; height: 30px'></dwc-icon>"

    REM /**
    REM  * Constructor for the TaskCardView
    REM  *
    REM  * @param BBjWindow parent! The parent window which contains all task child windows of a given view
    REM  * @param Task t!           The Task that is to be added to a given view
    REM  */
    method public TaskCardView(BBjWindow parent!, Task t!)
        #super!(parent!,t!)
        #win!.addClass("winCardView")
        #title!.addClass("textTitle")

        #btnLock! = #win!.addButton(#lock!)

        if (#task!.getComplete() = 0) then
            #btnLock!.setVisible(0)
            #setPriorityFlag()
        else
            #win!.setUserData(#btnLock!)
            #btnLock!.addClass("btnLock")
            #btnLock!.setCallback(BBjControl.ON_BUTTON_PUSH, #this!, "setLock")
        endif

        #btnSetComplete! = #win!.addButton("")

        #btnSetComplete!.addClass("btnSetComplete")
        #btnSetComplete!.setCallback(BBjControl.ON_BUTTON_PUSH, #this!, "setComplete")

        trashIcon! = "<html><dwc-icon pool='tabler' name='trash' style='width: 25px; height: 25px'></dwc-icon>"
        #btnDeleteTask! = #win!.addButton(trashIcon!)
        #btnDeleteTask!.addClass("btnDelete")
        #btnDeleteTask!.setCallback(BBjControl.ON_BUTTON_PUSH, #this!, "onDelete")

        #refresh()
    methodend

    REM /**
    REM  * Updates task data on refresh
    REM  */
    method public void refresh()
        #super!.refresh()

        if (#task!.getComplete()) then
            #win!.addClass("complete")
            #btnSetComplete!.setText("Mark as Incomplete")
            #win!.setToolTipText("Unlock to edit task")
            #btnLock!.addClass("btnLock")
            #btnLock!.removeClass("btnLockOpen")
            #btnLock!.setText(#lock!)
            if (#priorityFlag! <> null()) then #priorityFlag!.addClass("priority-transparent")
        else
            #win!.removeClass("complete")
            #win!.setToolTipText("Click to edit task")
            #btnSetComplete!.setText("Mark as Complete")
            #btnLock!.removeClass("btnLock")
            #btnLock!.addClass("btnLockOpen")
            #btnLock!.setText(#lockOpen!)
            if (#priorityFlag! <> null()) then #priorityFlag!.removeClass("priority-transparent")
        endif

        #description!.setToolTipText(#task!.getDescription())
        #title!.setToolTipText(#task!.getTitle())

    methodend

    REM /**
    REM  * - Toggles a completed task lock icon between open and closed
    REM  * - Icon is set to BBjStaticText #btnLock!
    REM  * - Toggles associated classes
    REM  *
    REM  * @param BBjButtonPushEvent e!     The BBjButtonPushEvent that causes this method to be executed
    REM  */
    method public void setLock(BBjButtonPushEvent e!)
        if (#btnLock!.getText() = #lock!) then
            #btnLock!.setText(#lockOpen!)
            #btnLock!.removeClass("btnLock")
            #btnLock!.addClass("btnLockOpen")
            #win!.removeClass("complete")
            #win!.setToolTipText("Click to edit task")
        else
            #btnLock!.setText(#lock!)
            #btnLock!.removeClass("btnLockOpen")
            #btnLock!.addClass("btnLock")
            #win!.addClass("complete")
            #win!.setToolTipText("Unlock to edit task")
        endif
    methodend

    REM /**
    REM  * Callback event for the task completion check box.  Sets the associated task to complete.
    REM  *
    REM  * @param BBjButtonPushEvent e!     The BBjButtonPushEvent that causes this method to be executed
    REM  */
    method public void setComplete(BBjButtonPushEvent e!)
         if (#task!.getComplete()) then
            #task!.setComplete(0)
        else
            #task!.setComplete(1)
        endif

        BBjAPI().postCustomEvent("TASK_UPDATE",#task!)
    methodend

    REM /**
    REM  * Triggers message box which prompts user for confirmation before deleting a task
    REM  *
    REM  * @param BBjButtonPushEvent e!     The BBjButtonPushEvent that causes this method to be executed
    REM  */
    method public void onDelete(BBjButtonPushEvent e!)
        #task!.setDeleted(1)
        BBjAPI().postCustomEvent("TASK_UPDATE",#task!)
    methodend
classend

REM BBj USE Statements
use ::Task.bbj::Task
use ::Utils.bbj::Utils
use ::TaskView.bbj::TaskView
use ::TaskApp.bbj::TaskApp
