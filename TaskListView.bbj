REM /**
REM  * TaskListView.bbj
REM  * @author gosteen
REM  * 
REM  * TaskListView represents the childwindow of a task in a list
REM  */

class public TaskListView extends TaskView
    field public    BBjCheckBox     checkbox!

    REM /**
    REM  * Constructor for the TaskListView
    REM  *
    REM  * @param BBjWindow parent! The parent window which contains all task child windows of a given view
    REM  * @param Task t!           The Task that is to be added to a given view
    REM  */
    method public TaskListView(BBjWindow parent!, Task t!)
        #super!(parent!,t!)
        #win!.addClass("winListView")
        #checkbox! = #win!.addCheckBox("")
        #checkbox!.addClass("checkbox")
        #checkbox!.setCallback(BBjControl.ON_CHECK_CHANGE, #this!, "onTaskCheck")
        #refresh()
    methodend


    REM /**
    REM  * Refresh the TaskListView with the latest information from its Task object.
    REM  * This is called from the TaskApp class when a TASK_UPDATE event is posted.
    REM  */
    method public void refresh()
        #super!.refresh()
        #checkbox!.setSelected(#task!.getComplete())
    methodend

    REM /**
    REM  * Method to run when a task's checkbox is changed
    REM  *
    REM  * @param BBjCheckChangeEvent e!    The BBjCheckChangeEvent that invoked this method
    REM  */
    method public void onTaskCheck(BBjCheckChangeEvent e!)
        #clearStyles()
        if (#task!.getComplete()) then
            #task!.setComplete(0)
        else
            #task!.setComplete(1)
        endif
        BBjAPI().postCustomEvent("TASK_UPDATE",#task!)
    methodend
classend

REM BBj USE Statements
use ::Task.bbj::Task
use ::Utils.bbj::Utils
use ::TaskView.bbj::TaskView