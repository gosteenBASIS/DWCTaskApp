REM /**
REM  * TaskListView.bbj
REM  * @author gosteen
REM  *C:\Users\gosteen\eclipse\workspaces\bbj-workspace\TaskApp\TaskListView.bbj
REM  * TaskListView represents the childwindow of a task in a list
REM  */

class public TaskListView
    field protected BBjSysGui       sysgui!
    field public    BBjChildWindow  win!
    field public    Task            task!
    field public    BBjStaticText   title!
    field public    BBjStaticText   description!
    field public    BBjNumber       priority!
    field public    BBjCheckBox     checkbox!
    field public    BBjString       uuid!
    field public    BBjStaticText   due!
    field public    BBjString       flagIcon!
    field public    BBjStaticText   priorityFlag!
    field public    BBjVector       flagVector!
    field public    BBjString       flagText!
    field public    BBjPopupMenu    flagPopupMenu!
    

    method public TaskListView(BBjWindow parent!, Task t!)
        #sysgui! = BBjAPI().getSysGui()
        #win! = parent!.addChildWindow("", $00108000$, #sysgui!.getAvailableContext())
        #win!.addClass("task clickable")
        #task! = t!
        #uuid! = #task!.getUuid()
        #win!.setAttribute("id", #uuid!)
        #checkbox! = #win!.addCheckBox("")
        #checkbox!.addClass("checkbox")
        #checkbox!.setCallback(BBjControl.ON_CHECK_CHANGE, #this!, "onTaskCheck")
        
        #priority! = #task!.getPriority()
        
        #title! = #win!.addStaticText("")
        #title!.addClass("title-display")   
        
        #due! = #win!.addStaticText("")   
        
        #win!.addStaticText("")
 
        #description! = #win!.addStaticText("")
        #description!.addClass("description-display")

        #createPopupMenu() 
        #priorityFlag! = #win!.addStaticText("") 
        #priorityFlag!.setPopupMenu(#flagPopupMenu!)
        #setPriorityFlag()
        
        #refresh()
    methodend
    
    method public void createPopupMenu()
        #flagPopupMenu! = #sysgui!.addPopupMenu()
        #flagPopupMenu!.addClass("flag-popup")

        deleteFlag! = "<html><bbj-icon pool='tabler' name='x' class='priority-0 menu-item' style='pointer-events: none;'></bbj-icon>"
        blueFlag! = "<html><bbj-icon pool='tabler' name='flag-2-filled' id='-1' class='priority-1 menu-item' style='pointer-events: none;'></bbj-icon>"
        greenFlag! = "<html><bbj-icon pool='tabler' name='flag-2-filled' id='-2' class='priority-2 menu-item' style='pointer-events: none;'></bbj-icon>"
        yellowFlag! = "<html><bbj-icon pool='tabler' name='flag-2-filled' id='-3' class='priority-3 menu-item' style='pointer-events: none;'></bbj-icon>"
        orangeFlag! = "<html><bbj-icon pool='tabler' name='flag-2-filled' id='-4' class='priority-4 menu-item' style='pointer-events: none;'></bbj-icon>"
        redFlag! = "<html><bbj-icon pool='tabler' name='flag-2-filled' id='-5' class='priority-5 menu-item' style='pointer-events: none;'></bbj-icon>"  

        #flagVector! = BBjAPI().makeVector()
        #flagVector!.add(deleteFlag!)
        #flagVector!.add(blueFlag!)
        #flagVector!.add(greenFlag!)
        #flagVector!.add(yellowFlag!)
        #flagVector!.add(orangeFlag!)
        #flagVector!.add(redFlag!)    
         
        i! = 0
        id! = -1
        while (i! < #flagVector!.size()) 
            currentPriority! = #task!.getPriority()
            flag! = #flagVector!.getItem(i!)
            
            if (i! = 0) then
                flagItem! = #flagPopupMenu!.addMenuItem(-6, flag!)
            else
                flagItem! = #flagPopupMenu!.addMenuItem(id!, flag!)
                if (currentPriority! = i!) then flagItem!.setVisible(0)
                id! = id! - 1
            endif
            
            flagItem!.setCallback(flagItem!.ON_POPUP_ITEM_SELECT, #this!, "changePriority")
            i! = i! + 1
        wend
    methodend
    
    method public void setDueNotice()
        #due!.removeClass("due-today past-due")       
        due! = ""
        
        if (#task!.getDueDate() > -1) AND (#task!.getDueDate() < jul(date(0))) then 
            due! = "Past Due!"
             #due!.addClass("due-today past-due")
        endif
        
        if (#task!.getDueDate() = jul(date(0))) then 
            due! = "Due Today!"
             #due!.addClass("due-today")
        endif
        
        #due!.setText(due!) 
    methodend
    
    method public void setPriorityFlag() 
        #flagText! = "Priority " + STR(#task!.getPriority())
         
        if (#task!.getPriority() > 0) then 
            #flagIcon! = "<html><bbj-icon pool='tabler' name='flag-2-filled'></bbj-icon>"
        else 
            #flagIcon! = ""
        endif    
    
        #priorityFlag!.setText(#flagIcon!)
        #priorityFlag!.removeClass("priority-1 priority-2 priority-3 priority-4 priority-5 priority-0")
        #priorityFlag!.addClass("priority-flag")
        #priorityFlag!.addClass("priority-" + STR(#task!.getPriority()))
        #priorityFlag!.setToolTipText(#flagText!)
    methodend

    method public void changePriority(BBjPopupSelectEvent e!)
        newPriority! = ABS(e!.getMenuItem().getID())
        
        if (newPriority! = 6) then newPriority! = 0
        
        #task!.setPriority(newPriority!)
        BBjAPI().postCustomEvent("TASK_UPDATE", #task!)
    methodend
    
    method public void clearStyles()
        #win!.removeClass("complete incomplete blocked deferred")
    methodend

    method public void refresh()
        #clearStyles()
        #title!.setText(#task!.getTitle())
        #description!.setText(#task!.getDescription())
        #checkbox!.setSelected(#task!.isComplete())
        #win!.addClass(#task!.getStatusString(#task!.getStatus()))
        #checkbox!.addClass(#task!.getStatusString(#task!.getStatus()))
        #setPriorityFlag()
        #setDueNotice() 
        for i = 1 to #flagPopupMenu!.getChildCount()
            menuItem! = #flagPopupMenu!.getMenuItem(i)
            menuItem!.setVisible(1)
            if (#task!.getPriority() = mod(i,5)) then
                menuItem!.setVisible(0)
            endif
        next i
    methodend

    method public void setVisible(BBjNumber v!)
        #win!.setVisible(v!)
    methodend

    rem /**
    rem  * Method to run when a task's checkbox is changed
    rem  *
    rem  * @param e!    A BBjCheckChangeEvent
    rem  */
    method public void onTaskCheck(BBjCheckChangeEvent e!)
        #clearStyles()
        if (#task!.isComplete()) then
            #task!.setStatus(#task!.STATUS_INCOMPLETE())
        else
            #task!.setStatus(#task!.STATUS_COMPLETE())
        endif
        BBjAPI().postCustomEvent("TASK_UPDATE",#task!)
    methodend
    
    
    
    
    
    
classend

use ::Task.bbj::Task