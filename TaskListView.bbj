REM /**
REM  * TaskListView.bbj
REM  * @author gosteen
REM  *C:\Users\gosteen\eclipse\workspaces\dwc-workspace\TaskApp\TaskListView.bbj
REM  * TaskListView represents the childwindow of a task in a list
REM  */

class public TaskListView extends TaskView
    field public    BBjCheckBox     checkbox!

    method public TaskListView(BBjWindow parent!, Task t!)
        #super!(parent!,t!)
        #win!.addClass("winListView")
        #checkbox! = #win!.addCheckBox("")
        #checkbox!.addClass("checkbox")
        #checkbox!.setCallback(BBjControl.ON_CHECK_CHANGE, #this!, "onTaskCheck")
        #refresh()
    methodend


    rem /**
    rem  * Refresh the TaskListView with the latest information from its Task object.
    rem  * This is called from the TaskApp class when a TASK_UPDATE event is posted.
    rem  */
    method public void refresh()
        #super!.refresh()
        #checkbox!.setSelected(#task!.getComplete())
    methodend

    rem /**
    rem  * Method to run when a task's checkbox is changed
    rem  *
    rem  * @param e!    A BBjCheckChangeEvent
    rem  */
    method public void onTaskCheck(BBjCheckChangeEvent e!)
        #clearStyles()
        if (#task!.getComplete()) then
            #task!.setComplete(0)
        else
            #task!.setComplete(1)
        endif
        BBjAPI().postCustomEvent("TASK_UPDATE",#task!)
        Utils.consoleLog("just posted task update custom event")
    methodend
classend

use ::Task.bbj::Task
use ::Utils.bbj::Utils
use ::TaskView.bbj::TaskView